plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.3'
    id "io.spring.dependency-management" version "1.1.0"
    id "org.owasp.dependencycheck" version "8.3.1"
}

sourceCompatibility = compatibility
targetCompatibility = compatibility

group = projectGroup
def env = System.getenv()
def buildNumberStr = env['BUILD_NUMBER']
def artifactVersion = baseVersion + (buildNumberStr != null ? "." + buildNumberStr : "-SNAPSHOT")
version = artifactVersion

// In this section you declare where to find the dependencies of your project
repositories {
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    mavenCentral()
}

jar {
    archiveBaseName.set(projectName)
    archiveVersion.set(artifactVersion)
    manifest {
        attributes 'Implementation-Title': projectDescription,
                'Implementation-Version': jar.archiveVersion.get()
    }
}

configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(bootJar)
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-devtools')

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

task buildDevImage(type: Exec) {
    dependsOn build
    commandLine 'docker', 'build', '--progress', 'plain', '--build-arg', "VERSION=${artifactVersion}", '-t', 'demo-policy', '.'
}

